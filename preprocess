#!/usr/bin/env bash

# preprocess: Clean and standardize dataset

if [[ $# -ne 1 ]]; then
    echo "Usage: $0 <input-file>" >&2
    exit 1
fi

infile="$1"

# Step 1: delete \r, replace ; with <TAB>, delete non-ASCII
tmp1=$(mktemp)
tr -d '\r' < "$infile" \
    | tr ';' '\t' \
    | tr -cd '\0-\177\n' > "$tmp1"

# Step 2: I noticed that columns 9 and 11 have “,” so we're going to treat them separately as “.”
tmp2=$(mktemp)
awk -F '\t' 'BEGIN { OFS = "\t" }
NR == 1 { print; next }
{
    gsub(",", ".", $9)    # Rating Average
    gsub(",", ".", $11)   # Complexity Average
    print
}' "$tmp1" > "$tmp2"

# Step 3: Create a new ID for an empty ID (first column)
awk -F '\t' '
BEGIN {
    OFS = "\t"
    max_id = 0
}

NR == 1 {
    print
    next
}

{
    if ($1 ~ /^[0-9]+$/ && $1 > max_id) {
        max_id = $1
    }
    lines[NR] = $0
}

END {
    new_id = max_id + 1
    for (i = 2; i <= NR; i++) {
        split(lines[i], fields, "\t")
        if (fields[1] == "") {
            fields[1] = new_id++
        }
        line = fields[1]                          # Reassemble a line
        for (j = 2; j <= length(fields); j++) {
            line = line OFS fields[j]
        }
        print line
    }
}
' "$tmp2"

rm "$tmp1" "$tmp2"
