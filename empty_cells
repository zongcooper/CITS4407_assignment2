#!/usr/bin/env bash

# empty_cells: Handling empty cells 

if [[ $# -ne 1 ]]; then                    # Check for exactly one argument
    echo "Usage: $0 <filename>" >&2
    exit 1
fi

filename="$1"

awk -F ";" '

# Read header line (column names)
NR==1 {
    for (i = 1; i <= NF; i++) {
        gsub(/\r/, "", $i)                 # Remove Windows CR if present
        title[i] = $i                      
        count[i] = 0                       
    }
    col_count = NF                         
    next
}
{
 # For each data row, check each expected column
    for (i = 1; i <= col_count; i++) {
        if(i<=NF){                         # Extreme situations: Avoid error if field is missing in this row
            gsub(/\r/, "", $i)             # Eliminate Windows Carriage Return \r Residue
            if ($i == "") {
                count[i]++
            }
        }else{
            count[i]++                     # Extreme situations: Field missing entirely (i > NF), count as empty
        }
    }
}
 # Print each column name with its count of empty cells
END {
    for (i = 1; i <= col_count; i++) {
        printf "%s: %d\n", title[i], count[i]
    }
}
' "$filename"
